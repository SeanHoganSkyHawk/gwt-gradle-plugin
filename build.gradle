buildscript {
    repositories {
    	jcenter()
		maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
    	classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
		classpath "com.gradle.publish:plugin-publish-plugin:0.10.0"
	}
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.gradle.plugin-publish';
apply plugin: 'maven-publish'
apply plugin: 'groovy'

group = 'de.esoco.gwt'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile gradleApi()
	compile "com.google.guava:guava:23.5-jre"
	testCompile 'org.spockframework:spock-core:1.3-groovy-2.5', gradleTestKit()
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
    from javadoc
}
task sourceJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourceJar
}

gradlePlugin {
	plugins {
		gwtPlugin {
			id = 'de.esoco.gwt'
			implementationClass = 'de.esoco.gwt.gradle.GwtPlugin'
		}
		gwtLibPlugin {
			id = 'de.esoco.gwt-lib'
			implementationClass = 'de.esoco.gwt.gradle.GwtLibPlugin'
		}

	}
}

pluginBundle {
	website = 'https://github.com/esoco/gwt-gradle-plugin'
	vcsUrl = 'https://github.com/esoco/gwt-gradle-plugin.git'
	description = 'GWT Gradle Plugin'
	tags = ['gwt', 'java']

	plugins {
		gwtPlugin {
			displayName = 'Gradle GWT plugin'
		}
		gwtLibPlugin {
			displayName = 'Gradle GWT lib plugin'
		}
	}
}

publishing {
    publications {
        pluginPublication (MavenPublication) {
            from    components.java
            groupId    project.group
            artifactId project.name
            version    project.version
        }
    }
}
// ----------- Bintray Release -----------

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'

bintray {
	user = project.hasProperty('bintrayUser') ?
				project.property('bintrayUser') :
				System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ?
				project.property('bintrayApiKey') :
				System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publish = true

	pkg {
		repo = 'esoco'
		name = project.name
		version {
			name = project.version
	    }
    }
}
